java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'tgbots-module'
            groupId = group

            from components.java

            pom {
                name = 'tgbots-module'
                description = 'Simple Java telegram bots runner built on top of the Telegram Bots library'
                url = 'https://github.com/aNNiMON/tgbots-module'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/license/mit'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'annimon'
                        name = 'Victor Melnik'
                    }
                }
                scm {
                    url = 'https://github.com/aNNiMON/tgbots-module'
                    connection = 'scm:git:git://github.com/aNNiMON/tgbots-module.git'
                    developerConnection = 'scm:git:ssh://git@github.com:aNNiMON/tgbots-module.git'
                }
                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/aNNiMON/tgbots-module/issues'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    gitRootSearch = true
    signing {

        pgp {
            active = 'ALWAYS'
            armored = true
        }
    }
    deploy {
        maven {
            mavenCentral {
                'release-deploy' {
                    active = 'RELEASE'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
            nexus2 {
                'snapshot-deploy' {
                    active = 'SNAPSHOT'
                    snapshotUrl = 'https://central.sonatype.com/repository/maven-snapshots/'
                    applyMavenCentralRules = true
                    snapshotSupported = true
                    closeRepository = false
                    releaseRepository = false
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}